# Elaborations for top-state and substates

# name
# Name a state using the selected superstate operator's name
sp {all*elaborate*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

# top-state
# Copy direct top-state link to all substates
sp {top-state*elaborate*top-state
   (state <s> ^superstate nil)
-->
   (<s> ^top-state <s>)
}
#duplicate of elaborate*state*top-state
#sp {all*elaborate*top-state*top-state
#   (state <s> ^superstate <ss>)
#   (<ss> ^top-state <ts>
#        -^operator.name evaluate-operator)
#-->
#   (<s> ^top-state <ts>)
#}

# io
# Copy the io link to all substates
sp {all*elaborate*io
   (state <s> ^superstate.io <io>
	      -^name selection
	      -^problem-space.state.dont-copy io
             -^name learn-param
             -^name setup-search
   	      -^name action-policy)
-->
   (<s> ^io <io>)
}

# agent-params
# Copy agent-params to all substates
sp {all*elaborate*agent-params
   (state <s> ^superstate.agent-params <ap>)
-->
   (<s> ^agent-params <ap>)
}

# Update world predicates on the output-link
sp {all*elaborate*output-link*world-predicate-relations
   (state <s> ^agent-params.update-world-predicates-timer 0
              ^top-state <ts>
              ^io.output-link <ol>)
   (<ts>  ^name rosie
          ^world.predicates <preds>)
-->
    (<ol> ^predicates <preds>)
}

# Update component learning progress in a task
sp {all*elaborate*output-link*component-learning-progress
   (state <s> ^agent-params.update-world-predicates-timer 2
              ^top-state <ts>
              ^io.output-link <ol>)
   (<ts>  ^name rosie
          ^learned-concepts <lns>)
-->
    (<ol> ^learned-concepts <lns>)
}
