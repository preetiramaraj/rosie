# This operator is proposed in order to add the auxiliary verb as a part of the object description based on rtype
# is for rtype = single and are for rtype = set

###################################################################################
# Propose add auxiliary verb to object description
##################################################################################

sp {dialog-event*propose*add-auxiliary-verb-to-obj-desc
   (state <s> #^name dialog-event
              ^PR-game-details yes
              ^descriptions <descs>
              -^combined
              ^generated yes)
   (<descs> ^obj-desc <obj-desc>)
   (<obj-desc> -^aux-verb)
   -{(<s> ^descriptions {> <descs> <descs2>})}
   -{ (<descs> ^obj-desc { > <obj-desc> <obj-desc-2> })}
-->
   (<s> ^operator <o> +)
   (<o> ^name add-auxiliary-verb-to-obj-desc)
}

sp {dialog-event*apply*add-auxiliary-verb-to-obj-desc*rtype-set
   (state <s> #^name dialog-event
              ^PR-game-details yes
              ^operator.name add-auxiliary-verb-to-obj-desc
              ^descriptions <descs>
              ^generated yes)
   (<descs> ^obj-desc <obj-desc>)
   (<obj-desc> ^rtype set)
#^1 <obj>)
#   (<obj> ^rtype set)
-->
   (<obj-desc> ^aux-verb |are |)
   (<s> ^added yes)
}

sp {dialog-event*apply*add-auxiliary-verb-to-obj-desc*rtype-single
   (state <s> #^name dialog-event
              ^PR-game-details yes
              ^operator.name add-auxiliary-verb-to-obj-desc
              ^descriptions <descs>
              ^generated yes)
   (<descs> ^obj-desc <obj-desc>)
   (<obj-desc> ^rtype single)
#^1 <obj>)
   #(<obj> ^rtype single)
-->
   (<obj-desc> ^aux-verb |is |)
   (<s> ^added yes)
}

######################################################################################
# Propose add personal pronoun it to concept definition
######################################################################################
# PR - TODO this assumes there are only two objects in question
# In case the condition is about a group of objects, it won't work
sp {dialog-event*propose*add-personal-pronoun
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              -^combined
              ^added yes
              ^generated yes)
   (<descs> ^description <desc>
            -^description <desc2> <> <desc>)
   (<desc> ^1 <param-id>
           ^2 id2)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-personal-pronoun)
}

sp {dialog-event*propose*add-personal-pronoun*input-arg
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              -^combined
              ^added yes
              ^generated yes)
   (<descs> ^obj-desc <obj-desc1>
            ^obj-desc { <> <obj-desc1> <obj-desc2> })
   (<obj-desc1> ^1 <obj1>)
   (<obj1> ^attribute input-arg)
   (<obj-desc2> ^1 <obj2>)
   (<obj2> -^attribute input-arg)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-personal-pronoun)
}

sp {dialog-event*propose*add-personal-pronoun*set-description
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              -^combined
              ^added yes
              ^generated yes)
   (<descs> ^description <desc>
            -^description <desc2> <> <desc>)
   (<desc> ^type set-description)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-personal-pronoun)
}

sp {dialog-event*apply*add-personal-pronoun
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              ^generated yes
              ^operator.name add-personal-pronoun)
-->
   (<descs> ^pronoun |it|)
}

sp {dialog-event*propose*add-personal-pronoun*none
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              -^combined
              ^added yes
              ^generated yes)
   (<descs> ^obj-desc <obj-desc1>
            ^obj-desc { <> <obj-desc1> <obj-desc2> })
   (<obj-desc1> ^1 <obj1>)
   (<obj1> ^attribute input-arg)
   (<obj-desc2> ^1 <obj2>)
   (<obj2> ^attribute input-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-personal-pronoun-none)
}

sp {dialog-event*apply*add-personal-pronoun*none
   (state <s> ^name dialog-event
              ^purpose.type agent-game-concept-definition
              ^descriptions <descs>
              ^generated yes
              ^operator.name add-personal-pronoun-none)
-->
   (<descs> ^pronoun |none|)
}