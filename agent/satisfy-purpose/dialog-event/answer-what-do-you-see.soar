# PR - TODO either don't add the person or edit something so that it treats a person it sees differently from things it can see in the room
sp {dialog-event*propose*answer-what-do-you-see
   (state <s> ^name dialog-event
              -^generated
              -^combined
              ^purpose <p>)
   (<p> ^type agent-perception-description
        ^parameters <params>
        ^satisfaction <sat>)   
   (<params> ^predicates <preds>
             ^objects <objs>) # May not work with H-layout
-->
   (<s> ^operator <o> +)
   (<o> ^name answer-what-do-you-see
        ^predicates <preds>
        ^objects <objs>)
}

sp {dialog-event*apply*answer-what-do-you-see*add-descriptions-to-state
   (state <s> ^name dialog-event
              ^operator.name answer-what-do-you-see)
-->
   (<s> ^descriptions <descs>)
}

sp {dialog-event*apply*answer-what-do-you-see*retrieve-PP-predicate
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>)
   (<o> ^name answer-what-do-you-see
        ^predicates <preds>)
   (<preds> ^predicate <pred>)
   (<pred> ^handle <prep>
           ^instance <inst>)
   (<inst> ^1 <obj1>
           ^2 <obj2>)
-->
   (<descs> ^description <desc>)
   (<desc> ^prep <prep>
           ^1 <obj1>
           ^2 <obj2>)
}

sp {dialog-event*apply*answer-what-do-you-see*retrieve-objects-not-part-of-predicates
   (state <s> ^name dialog-event
              ^descriptions <descs>
              ^operator <o>)
   (<o> ^name answer-what-do-you-see
        ^predicates <preds>
        ^objects <objs>)
   (<objs> ^object <obj1>)
   -{(<preds> ^predicate <pred>)
     (<pred> ^instance <inst>)
     (<inst> ^<< 1 2 >> <obj1>)}
-->
   (<descs> ^obj <obj1>)
}

########################################################
# Report answer using complete-answer-what-do-you-see
########################################################   
sp {dialog-event*propose*complete-answer-what-do-you-see
   (state <s> ^name dialog-event
              ^purpose.type agent-perception-description
              ^generated <gen>
              ^combined
              ^descriptions <descs>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-answer-what-do-you-see
        ^descriptions <descs>
        ^generated <gen>)   
}

sp {dialog-event*apply*complete-answer-what-do-you-see*send-message
   (state <s> ^name dialog-event
              ^purpose.type <type>
              ^operator <o>
              ^io.output-link <out>)
   (<o> ^name complete-answer-what-do-you-see
        ^descriptions <descs>
        ^generated <gen>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type <type>
          ^fields.descriptions <descs>)
   (<descs> ^generated <gen>)
}
 
sp {dialog-event*apply*complete-answer-what-do-you-see*mark*performed
   (state <s> ^name dialog-event
              ^operator.name complete-answer-what-do-you-see
              ^io.output-link.send-message <msg>
              ^top-state.interaction.status <status>)
-->
   (<status> ^performed.dialog-event <de>)
   (<de> ^type explanation
         ^originator agent)
}